const std = @import("std");
const pumpkin = @import("pumpkin");
const cmd = pumpkin.cmd;
const msg = pumpkin.msg;

pub const std_options: std.Options = .{ .logFn = pumpkin.logFn };

const log = std.log.scoped(.example);

const Example = struct {
    pub const metadata: pumpkin.Metadata = .{
        .name = "pumpkin-plugin-example",
        .version = "0.1.0",
        .authors = &.{"You"},
        .description = "An example plugin in Zig.",
    };

    pub const events = .{
        .player_join_event = onJoin,
        .player_chat_event = .{ onChat, pumpkin.EventOptions{ .priority = .high } },
    };

    // /greet and /greet <targets>
    pub const commands: []const cmd.Spec = &.{.{
        .names = &.{ "greet", "hi" },
        .description = "Says hello.",
        .permission = "pumpkin-plugin-example:command.greet",
        .run = greetSender,
        .then = &.{
            .argument("targets", .players, .{ .run = greetTargets }),
        },
    }};

    pub fn onLoad(_: pumpkin.Context) !void {
        log.info("Hello from Zig!", .{});
    }

    fn onJoin(_: pumpkin.Server, ev: *pumpkin.EventData(.player_join_event)) void {
        log.info("{s} joined", .{ev.player.getName()});
        ev.join_message = msg.build(&.{
            .{ .text = "+ ", .color = .green, .bold = true },
            .{ .text = ev.player.getName(), .color = .yellow },
        });
    }

    fn onChat(_: pumpkin.Server, ev: *pumpkin.EventData(.player_chat_event)) void {
        if (std.mem.indexOf(u8, ev.message, "spoiler") != null) ev.cancelled = true;
    }

    fn greetSender(sender: cmd.Sender, _: pumpkin.Server, _: cmd.Args) cmd.Result {
        sender.sendMessage(msg.build(&.{
            .{ .text = "Hello from Zig! ", .color = .gold },
            .{ .text = "[again]", .color = .gray, .click = .{ .run_command = "/greet" }, .hover = "Runs /greet" },
        }));
        return cmd.ok(1);
    }

    fn greetTargets(sender: cmd.Sender, _: pumpkin.Server, args: cmd.Args) cmd.Result {
        const targets = cmd.get(args, "targets", .players) orelse return cmd.fail("nobody to greet");
        for (targets) |target| {
            target.sendSystemMessage(msg.fmt(.aqua, "{s} says hello", .{sender.getName()}), false);
        }
        sender.sendMessage(msg.fmt(null, "Greeted {d} player(s).", .{targets.len}));
        return cmd.ok(@intCast(targets.len));
    }
};

comptime {
    pumpkin.register(Example);
}
